{ const typed = (type, value, raw) => ({type, value, ...(raw ? {raw} : {})}) }

CellParser 
  = _ '=' _ value:Execute _ { return typed('execute', value) }
  / _ value:TypedFloat _ !('.'+ / [^0-9]) _ { return value }
  / _ value:TypedInteger _ !('.'+ / [^0-9]) _ { return value }
  / TypedString


TypedFloat = _ float:Float _ { return typed('float', float) }
TypedInteger = _ integer:Integer _ { return typed('integer', integer) }
TypedString = _ string:String _ { return typed('string', string) }

Execute = query:(Factor/Command/Address) {
  return query
}

String = val:(.*) { return val.join('') }

Integer = int:([0-9,])+ _ {
  return parseInt(int.join('').replace(',', ''))
}

word = word:(letter+) {
  return word.join('')
}
letter = [a-zA-Z0-9]

Operation = op:('+'/'-'/'*'/'/'/'^') _ value:Factor {
  return {
    type: 'operation',
    op,
    value
  }
}
Factor = input:Primary _ operations:(Operation)* {
  if (!operations || !operations.length) {
    return input
  }
  return {type: 'compute', input, operations}
}
Primary = '(' val:Factor ')' { return val }
  / TypedFloat
  / TypedInteger
  / Arg

Number = int:([0-9,]+) { return parseInt(int.join('').replace(',', '')) }
Float = left:([0-9,]+)? '.' right:([0-9]+) {
  return parseFloat(left.join('').replace(',', '') + '.' + right.join(''))
}

Args = args:([,]? _ arg:(Factor/Arg) _ { return arg })* {
	return args
}

Command = comm:(word) _ "(" _ args:(Args) _ ")" {
  return {
    type: 'command',
    comm,
    args
  }
}

Arg = arg:(Range / AddressProperty / Address / Command / TypedValue) { return arg; }

Address = colFix:([$]?) col:([a-zA-Z]+)? '' rowFix:([$]?) row:([0-9]+) property:AddressProperty* {
  col = col && col.join('')
  row = parseInt(row.join(''))
  colFix = !!colFix
  rowFix = !!rowFix
	return {type: 'address', row, col, colFix, rowFix, address: col + row, ...(property.length ? {property} : {}) }
}

AddressProperty = '.' _ property:(TypedInteger/!Address val:[a-zA-Z0-9]+ { return {type: 'string', value: val.join('')} }/Address) {
  console.log('. Property:', property)
	return property
}

Range = from:Address ':' to:Address {
  return {

    type: 'range',
    from,
    to
  }
}

TypedValue = value:Value { return typed('literal', value, text()) }

Value
  = '"' chars:DoubleStringCharacter* '"' { return chars.join(''); }
  / "'" chars:SingleStringCharacter* "'" { return chars.join(''); }

DoubleStringCharacter
  = !('"' / "\\") char:. { return char; }
  / "\\" sequence:EscapeSequence { return sequence; }

SingleStringCharacter
  = !("'" / "\\") char:. { return char; }
  / "\\" sequence:EscapeSequence { return sequence; }

EscapeSequence
  = "'"
  / '"'
  / "\\"
  / "b"  { return "\\b";   }
  / "f"  { return "\\f";   }
  / "n"  { return "\\n";   }
  / "r"  { return "\\r";   }
  / "t"  { return "\\t";   }
  / "v"  { return "\\x0B"; }
 
space = " "
_ = [ \t\n\r]*
